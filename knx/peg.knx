($provide! (peg/matches? peg/match) ($sequence
    ($define! helper-capturing ($lambda (peg text-chars) // #false | (captures captured-chars remaining-chars)
        ($match peg
            ((#constant value)                  ($sequence (debugLog value value) (list (list value) (list) text-chars)))
            ((#capture inner-peg)               ($match (helper-capturing inner-peg text-chars)
                                                    (#false #false)
                                                    ((inner-captures captured-chars remaining-chars)
                                                            (list (withTail inner-captures (join captured-chars)) captured-chars remaining-chars))))
            ((#group inner-peg)                 ($match (helper-capturing inner-peg text-chars)
                                                    (#false #false)
                                                    ((inner-captures captured-chars remaining-chars)
                                                            (list (list inner-captures) captured-chars remaining-chars))))
            
            (_ (atom? peg)                      ($let ((remaining-peg (chars peg)))
                                                    ($cond 
                                                        ((empty? remaining-peg) (list (list) (list) text-chars))
                                                        ((empty? text-chars) #false)
                                                        ((not (=? (first remaining-peg) (first text-chars))) #false)
                                                        (#true ($match (helper-capturing (join (rest remaining-peg)) (rest text-chars))
                                                            (#false #false)
                                                            ((inner-captures captured-chars remaining-chars)
                                                                (list inner-captures remaining-peg remaining-chars)))))))
            
            ((#space)                           (helper-capturing SPACE text-chars))
            ((#newline)                         (helper-capturing NEWLINE text-chars))
            ((#tab)                             (helper-capturing TAB text-chars))
            ((#open-paren)                      (helper-capturing OPEN_PAREN text-chars))
            ((#close-paren)                     (helper-capturing CLOSE_PAREN text-chars))
            ((#whitespace)                      (helper-capturing (list #set (join* SPACE NEWLINE TAB)) text-chars))
            ((#digit)                           (helper-capturing (list #set #0123456789) text-chars))

            ((#char)                            ($cond 
                                                    ((empty? text-chars) #false)
                                                    (#true (list (list) (list (first text-chars)) (rest text-chars)))))

            ((#set chars-set)                   ($cond 
                                                    ((empty? text-chars) #false)
                                                    ((in? (first text-chars) (chars chars-set)) (list (list) (list (first text-chars)) (rest text-chars)))
                                                    (#true #false)))
            
            ((#sequence . rest-of-pegs)         ($if (=? #0 (len rest-of-pegs))
                                                    (list (list) (list) text-chars)  
                                                    ($match (helper-capturing (first rest-of-pegs) text-chars)
                                                        (#false #false)
                                                        ((first-inner-captures first-captured-chars first-remaining-chars)
                                                            ($match (helper-capturing (withHead #sequence (rest rest-of-pegs)) first-remaining-chars)
                                                                (#false #false)
                                                                ((rest-inner-captures rest-captured-chars rest-remaining-chars)
                                                                    (list 
                                                                        (concat first-inner-captures rest-inner-captures) 
                                                                        (concat first-captured-chars rest-captured-chars) 
                                                                        rest-remaining-chars)))))))
            
            ((#choice . rest-of-pegs)           ($if (=? #0 (len rest-of-pegs))
                                                    #false
                                                    ($match (helper-capturing (first rest-of-pegs) text-chars)
                                                        (#false (helper-capturing (withHead #choice (rest rest-of-pegs)) text-chars))
                                                        (result result))))
            
            ((#between lower upper inner-peg)   ($if (<? upper #0)
                                                    #false
                                                    ($match (helper-capturing inner-peg text-chars)
                                                        (#false ($if (<? lower #1) (list (list) (list) text-chars) #false))
                                                        ((first-inner-captures first-captured-chars first-remaining-chars)
                                                            ($match (helper-capturing (list #between (- lower #1) (- upper #1) inner-peg) first-remaining-chars)
                                                                (#false #false)
                                                                ((rest-inner-captures rest-captured-chars rest-remaining-chars)
                                                                    (list 
                                                                        (concat first-inner-captures rest-inner-captures) 
                                                                        (concat first-captured-chars rest-captured-chars) 
                                                                        rest-remaining-chars)))))))

            ((#any inner-peg)                   ($match (helper-capturing inner-peg text-chars)
                                                    (#false     (list (list) (list) text-chars))
                                                    ((first-inner-captures first-captured-chars first-remaining-chars)
                                                        ($match (helper-capturing (list #any inner-peg) first-remaining-chars)
                                                            (#false     (list first-inner-captures first-captured-chars first-remaining-chars))
                                                            ((rest-inner-captures rest-captured-chars rest-remaining-chars)
                                                                (list 
                                                                    (concat first-inner-captures rest-inner-captures) 
                                                                    (concat first-captured-chars rest-captured-chars) 
                                                                    rest-remaining-chars))))))
            
            ((#some inner-peg)                  (helper-capturing (list #sequence inner-peg (list #any inner-peg)) text-chars))
        )
        ))
    ($define! peg/matches? ($lambda (peg text)
        (not (=? #false (helper-capturing peg (chars text))))))
    ($define! peg/match ($lambda (peg text)
        ($match (helper-capturing peg (chars text))
            (#false #false)
            ((inner-captures captured-chars remaining-chars) inner-captures))))))